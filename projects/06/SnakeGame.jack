
class SnakeGame {
   field Snake snake;
   field Fruit fruit;
   field int score, scoreInc;

   /** Construye un nuevo Juego de la Serpiente. */
   constructor SnakeGame new() {
      // Creates a 16 by 16 pixels snake
      let snake = Snake.new(0, 0, 16);
      let fruit = Fruit.new(128, 128, 16);
      let score = 0;
		let scoreInc = 50;
      return this;
   }
   
   /** Ejecuta el juego: lÃ³gica principal del juego */
   method void run() {
      var char key;  // the currently pressed key
      var boolean exit;
      let exit = false;

      while (~exit) {			
         // waits for a key to be pressed
         let key = Keyboard.keyPressed();
			// move snake one square
         do snake.move();
         if (~(snake.crashed()) & ~((key = 81)) ) {	// q key
				// change direction if arrow keys pressed         
				if ((key > 129) & (key < 134)) {
					do snake.setDirection(key - 129);
				}			
				if (snake.crashesWith(fruit)) {
					if (score = 0) { do Random.seed(scoreInc); }
					let score = score + Math.max(5, scoreInc);
					let scoreInc = 50;
					do snake.incrementLength();
					do fruit.place();
				}
				let scoreInc = scoreInc - 1;
				do snake.draw();		// draw snake
				do Sys.wait(150);  	// delay the next movement
			} 
			else {
				let exit = true;
			}
	   }
	   
	   do Output.moveCursor(11, 26); 	// center score
	   do Output.printString("SCORE: ");
	   do Output.printInt(score);
		return;
	}

   /** Elimina el juego. */
   method void dispose() {
      do snake.dispose();
      do Memory.deAlloc(this);
      return;
   }	
}
